/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

options
{
    MULTI = true;
    NODE_DEFAULT_VOID = true;
    STATIC = false;
}

PARSER_BEGIN(RuleExpression)

/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

package com.aws.greengrass.device.configuration.parser;

public class RuleExpression {
}
PARSER_END(RuleExpression)

SKIP :
{
    " "
|   "\r"
|   "\t"
}
TOKEN:
{
    < OR:           "OR" >
|   < AND:          "AND" >
|   < THINGNAME:    (<ALPHANUMERIC> | "-" | "_" | "\\:")+ > // Only allow escaped colons
|   < ALPHANUMERIC: [ "a"-"z" ] | [ "A"-"Z" ] | [ "0"-"9" ] >
}

ASTStart Start() #Start : {}
{
    expression() <EOF>
    { return jjtThis; }
}

void expression(): {}
{
    logicalORExpression()
}

void logicalORExpression(): {}
{
  (
    logicalANDExpression() [ <OR> logicalORExpression() ]
  ) #Or(>1)
}

void logicalANDExpression(): {}
{
  (
    unaryExpression() [ <AND> logicalANDExpression() ]
  ) #And(>1)
}

void unaryExpression(): {}
{
    // Only Thing expressions, to start
    thingExpression()
}

void thingExpression() #Thing :
{
    Token t;
}
{
    "thingName:" t=<THINGNAME>
    {
        jjtThis.value = t.image;
    }
}